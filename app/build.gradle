buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.jakewharton.hugo:hugo-plugin:1.1.0'
        classpath 'com.android.tools.build:gradle:1.0.0'
    }
}

repositories {
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

apply plugin: 'com.android.application'
apply plugin: 'hugo'

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.1"

    defaultConfig {
        applicationId "com.example.rennt.arcticsunrise"
        minSdkVersion 15
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"

        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
    }
    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        debug { java.srcDirs = ['src/debug/java', 'src/debug/java/'] }
        release { java.srcDirs = ['src/release/java', 'src/release/java/'] }
    }
    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
}


dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:21.0.2'
    compile 'com.android.support:recyclerview-v7:21.0.2'
    compile 'com.android.support:cardview-v7:21.0.2'
    compile 'com.android.support:support-v13:21.0.2'

    compile 'io.reactivex:rxandroid:0.23.0'
    compile 'com.squareup.okhttp:okhttp:2.1.0'

    compile 'com.astuetz:pagerslidingtabstrip:1.0.1'

    compile 'com.jakewharton:butterknife:6.0.0'

    compile 'com.github.satyan:sugar:1.3'

    compile 'com.melnykov:floatingactionbutton:1.1.0'

    // adds layout managers for recyclerview
    compile 'org.lucasr.twowayview:core:1.0.0-SNAPSHOT@aar'
    compile 'org.lucasr.twowayview:layouts:1.0.0-SNAPSHOT@aar'

    compile 'com.squareup.dagger:dagger:1.2.2'
    compile 'com.squareup.dagger:dagger-compiler:1.2.2'
    compile 'com.jakewharton.timber:timber:2.5.0'
    compile 'com.mcxiaoke.volley:library:1.0.7'

    compile 'com.google.code.gson:gson:2.3'
    compile('org.simpleframework:simple-xml:2.7.1') {
        exclude group: 'stax', module: 'stax-api'
        exclude group: 'xpp3', module: 'xpp3'
    }
}
